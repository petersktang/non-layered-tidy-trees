import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15
compileJava.options.encoding = "UTF-8"

ext {
    swtVersion = "3.117.0"
}

application {
    mainClass = "treelayout.swt.TestInterface"
}
switch ( OperatingSystem.current() ) {
    case OperatingSystem.WINDOWS:
        project.ext.swtNatives = "org.eclipse.swt.win32.win32.x86_64"
        sourceSets.main.java {
            exclude "**/PlatformLinux*.java", "**/PlatformMacOSX*.java"
        }
        sourceSets.test.java.srcDirs "test"
        break
    case OperatingSystem.LINUX:
        project.ext.swtNatives = "org.eclipse.swt.gtk.linux.x86_64"
        sourceSets.main.java {
            exclude "**/PlatformWin32*.java", "**/PlatformMacOSX*.java"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.swtNatives = "org.eclipse.swt.cocoa.macosx.x86_64"
        sourceSets.main.java {
            exclude "**/PlatformWin32*.java", "**/PlatformLinux*.java"
        }
        break
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            // The maven property ${osgi.platform} is not handled by Gradle
            // so we replace the dependency, using the osgi platform from the project settings
            def os = System.getProperty("os.name").toLowerCase()
            if (os.contains("windows")) {
                substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.win32.win32.x86_64:3.108.0")
            }
            else if (os.contains("linux")) {
                substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.gtk.linux.x86_64:3.108.0")
            }
            else if (os.contains("mac")) {
                substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.cocoa.macosx.x86_64:3.108.0")
            }
        }
    }
}

dependencies {
    implementation "org.eclipse.platform:$swtNatives:$swtVersion"
}
